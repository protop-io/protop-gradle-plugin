facets {
  functionalTest {
    parentSourceSet = 'test'
  }
}

compileFunctionalTestJava {
  options.compilerArgs += ['--enable-preview']
}

final envProtopHome = System.getenv('PROTOP_HOME')
final defaultProtopHome = "${rootDir}/protop/build" as String
final protopHome = envProtopHome ?: defaultProtopHome
final shouldUseLocalProtopExecutable = (protopHome == defaultProtopHome)

functionalTest {
  final protopSubmodule = gradle.includedBuild('protop')

  if (shouldUseLocalProtopExecutable) {
    dependsOn protopSubmodule.task(':executable')
  }

  jvmArgs += ['--enable-preview']

  environment 'PROTOP_HOME', protopHome

  doFirst {
    println "`:functionalTest` using `${protopHome}/bin/protop`." +
        (shouldUseLocalProtopExecutable
            ? '  Set `PROTOP_HOME` to override.'
            : '')
  }
}
